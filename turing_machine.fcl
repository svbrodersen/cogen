start: Q := '('(0: if 0 goto 3) '(1: right) '(2: goto 0) '(3: write 1));
      goto init;
init: Qtail := Q;
      Left := '();
      goto loop;
loop: if Qtail = '() goto stop else cont;
cont: Instruction := hd(Qtail);
      Qtail := tl(Qtail);
      Operator := hd(tl(Instruction));
      if Operator = 'right goto do_right else cont1;
cont1: if Operator = 'left goto do_left else cont2;
cont2: if Operator = 'write goto do_write else cont3;
cont3: if Operator = 'goto goto do_goto else cont4;
cont4: if Operator = 'if goto do_if else error;

do_right: Left := cons(hd(Right), Left);
          Right := tl(Right);
          goto loop;
do_left:  Right := cons(hd(Left), Right);
          Left := tl(Left);
          goto loop;
do_write: Symbol := hd(tl(tl(Instruction)));
          Right := cons(Symbol,tl(Right));
          goto loop;
do_goto:  Nextlabel := hd(tl(tl(Instruction)));
          Qtail := new_tail(Nextlabel, Q);
          goto loop;
do_if:    Symbol := hd(tl(tl(Instruction)));
          Nextlabel := hd(tl(tl(tl(tl(Instruction)))));
          if Symbol = hd(Right) goto jump else loop;
jump: Qtail := new_tail(Nextlabel, Q); 
      goto loop;
error: return o('('syntax-error:), Instruction);
stop: return right;
